module std_string uses std_base

// ======================================================
// String
// ======================================================

struct String {
    val: string
} is Equatable, Comparable, Stringer

fn String::value() -> string {
    return self.val
}

fn String::equals(other: object) -> bool {
    if other is String == false {
        return false
    }
    return self.val == ((String) other).val
}

fn String::hashCode() -> int {
    var hash: int = 31
    var i: int = 0
    for ch: byte in self.val {
        hash = hash ^ ((int) ch << i)
        i = (i + 8) % 24
    }
    return hash
}

fn String::compareTo(other: object) -> int {
    if other is String == false {
        panic -1
    }
    let peer: String = (String) other
    if self.val > peer.val {
        return 1
    }
    if self.val < peer.val {
        return -1
    }
    return 0
}

fn String::toString() -> string {
    return self.val
}

// ======================================================
// StringBuilder
// ======================================================

struct StringBuilder {
    strings: string list
    length: int
} is Stringer

fn StringBuilder::append(s: string) -> StringBuilder {
    self.strings.add(s)
    if s != nil {
        self.length = self.length + s.size()
    }
    return self
}

fn StringBuilder::toString() -> string {
    let buffer: byte[] = new byte[self.length + 1]
    var i: int = 0
    for s: string in self.strings {
        for ch: byte in s {
            if ch != (byte) 0 {
                buffer[i] = ch
                i = i + 1
            }
        }
    }
    buffer[i] = (byte) 0
    return (string) buffer
}


// ======================================================
// Strings
// ======================================================

struct StringMethods {
    emptyString: string
    brStr: string
    spaceStr: string
}

let Strings: StringMethods = new StringMethods {
    emptyString = ""
    brStr = (string) new byte[] { (byte) 10, (byte) 0 }
    spaceStr = " "
}

fn StringMethods::empty() -> string {
    return self.emptyString
}

fn StringMethods::br() -> string {
    return self.brStr
}

fn StringMethods::space() -> string {
    return self.spaceStr
}

fn StringMethods::newBuilder() -> StringBuilder {
    return new StringBuilder {
        strings = new string list {}
        length = 0
    }
}

fn StringMethods::newBuilderWith(s: string) -> StringBuilder {
    return new StringBuilder {
        strings = new string list { s }
        length = s.length() if s != nil else 0
    }
}

fn StringMethods::box(s: string) -> String {
    return new String {
        val = s
    }
}
